plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'com.palantir.git-version' version '0.11.0'
}

ext {
    // Project constants
    github_org = 'austinv11'
    project_name = 'Syringe'
    artifact_group = 'com.austinv11.aspects'
    project_version = '1.0.0-SNAPSHOT'
    project_description = 'Abstractions for programmatically modifying Java classes.'
    project_jdk = '1.8'

    // Dependencies
    jsr305_version = '3.0.2'
    junit_version = '4.12'
    logback_version = '1.3.0-alpha4'
    autoservice_version = '1.0-rc4'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group = artifact_group
    version = project_version
    description = project_description

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    task downloadDependencies() {
        description 'Download all dependencies to the Gradle cache'
        doLast {
            configurations.findAll { it.canBeResolved }.files
        }
    }

    javadoc.options.encoding = 'UTF-8'

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
        publications = ['maven']
        dryRun = false
        publish = true
        pkg {
            userOrg = 'austinv11'
            repo = 'maven'
            name = 'Syringe'
            websiteUrl = 'https://github.com/austinv11/Syringe'
            issueTrackerUrl = 'https://github.com/austinv11/Syringe/issues'
            vcsUrl = 'https://github.com/austinv11/Syringe.git'
            licenses = ['LGPL-3.0']
            labels = ['aspects', 'ast', 'asm', 'reflection', 'proxy', 'inject']
            publicDownloadNumbers = true
            attributes = [:]
            version {
                name = project.version
                vcsTag = project.version
                gpg {
                    sign = true
                }
                if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                    def sonatypeUsername = project.property('sonatypeUsername')
                    def sonatypePassword = project.property('sonatypePassword')
                    mavenCentralSync {
                        user = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'com.palantir.git-version'

    archivesBaseName = 'syringe-' + project.name

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifactId archivesBaseName
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    task writeGitProperties(type: WriteProperties) {
        def details = versionDetails()
        File gitPropertiesDir = new File(project.buildDir, 'resources/main')
        File gitPropertiesFile = new File(gitPropertiesDir, 'git.properties')
        outputFile gitPropertiesFile
        inputs.dir project.files(new File(project.rootDir, '.git')).asFileTree
        def describe = details.lastTag + '-' + details.commitDistance + '-g' + details.gitHash.substring(0, 7)
        property 'application.name', 'Syringe'
        property 'application.version', project.version
        property 'application.description', project.description
        property 'application.url', 'https://github.com/austinv11/Syringe'
        property 'git.branch', details.branchName ?: 'none'
        property 'git.commit.id', details.gitHashFull
        property 'git.commit.id.abbrev', details.gitHash
        property 'git.closest.tag.name', details.lastTag ?: 'none'
        property 'git.closest.tag.commit.count', details.commitDistance
        property 'git.commit.id.describe', describe
    }

    project.getTasks().getByName(JavaPlugin.CLASSES_TASK_NAME).dependsOn writeGitProperties
}

publishing {
    publications {
        maven(MavenPublication) {
        }
    }
}

bintrayUpload.dependsOn build

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-4.7-all.zip'
}